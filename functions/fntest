# Test function for lib/fn.zsh library
# Use 'fntest --help' for more information.

local -A _fn=(
    [info]="Test function demonstrating fn.zsh library"
    [desc]="fntest function. This function is intended to showcase how to use the
            fn.zsh library for parsing command-line options and arguments in Zsh scripts."
    [version]="0.2.0"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
    [notes]="This is a test function and does not perform any real operations.
           It simply demonstrates the usage of the fn.zsh library."
)

local -a _fn_args=(
    "input|Input file|r"
    "times|How many times to run|r|integer"
    "output|Output file|o"
)

local -a _fn_opts=(
    # -h/--help and -v/--version are auto-added (because _fn[version] is set)
    "cycles|c|Number of cycles|n|integer[1;10]"     # option with integer in range 1-10
    "force|f"                                       # option without description
    "test||Test without short name|txt|string[1;)"  # option with no short name
    "speed|s|Maximum speed|kmh|float(0;]"           # option with float > 0
    "quiet|q|Suppress output messages"
    "ip|i|Specify an IP address|ip|ipv4"             # option with IP address validation
    "email|e|User email address|email|email"         # option with email validation
    "url|u|Generic URL|url|url"                      # option with URL validation and forced https schema
    "website|w|Website URL|www|url[https]"           # option with URL validation and forced https schema
)

local -a _fn_examples=(
    "fntest input.txt|Basic usage with input file only"
    "fntest -c 8 input.txt output.txt"
    "fntest --force --quiet data.csv results.csv"
)

## Initialize

local -A opts=() args=()
_fn_init "$@" || return $REPLY

## Main logic - now it's simple!

printc "Options:" $y
print "  Number of possible options: $y${#_fn_opts}$x"
print "  Number of used options: $y${#opts}$x"

if (( #opts )); then
    print "  Used options and their values:"
    for opt in "${(@k)opts}"; do
        print -n -- "    ${y}•$x $c$opt$x"
        if [[ -n ${opts[$opt]} ]]; then
            print " => $y'$x$c${opts[$opt]}$x$y'$x"
        else
            print " (flag)$x"
        fi
    done
fi

printc "Arguments:" $y
print "  Number of possible arguments: $y${#_fn_args}$x"
print "  Number of required arguments: $y$(_fn_count_required_args)$x"
print "  Number of used arguments: $y${#args}$x"

if (( #args )); then
    print "  Used arguments and their values:"
    for arg in "${(@k)args}"; do
        print "    ${y}•$x $c$arg$x => $y'$x$c${args[$arg]}$x$y'$x"
    done
fi

return 0
