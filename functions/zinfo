# Display help information for a function from lib/ or functions/
# Use 'zinfo --help' for more information.

local -A _fn=(
    [info]="Display help information for a zconfig function"
    [desc]="Searches for a function in the functions/ directory or lib/*.zsh files
            and displays its documentation comments."
    [version]="0.3.5"
    [created]="2025"
    [modified]="2026-02-02"
    [license]="MIT"
    [author]="Andrzej Barabasz"
    [notes]="Extracts raw comments directly from source code. Most useful for helper
            functions in lib/. For interactive user functions, try running the function
            with ${p}--help${x} flag instead for formatted documentation."
)

local -a _fn_args=(
    "function|Function name to look up|r"
)

local -a _fn_examples=(
    "zinfo printe|Show help for printe function"
    "zinfo zfiles|Show help for zfiles function"
)

local -A opts=() args=()
_fn_init "$@" || return $REPLY

local func_name="${args[function]}"
local zsh_dir="${ZDOTDIR:-${XDG_CONFIG_HOME:-$HOME/.config}/zsh}"
local lib_dir="$zsh_dir/lib"
local func_dir="$zsh_dir/functions"

# Check if it's a function from /functions directory
if [[ -f "$func_dir/$func_name" ]]; then
    print "${g}${func_name}${x} is a function from ${c}/functions/${func_name}${x}"

    local line first=1
    while IFS= read -r line; do
        [[ "$line" != \#* ]] && break
        line="${line#\#}"
        line="${line# }"
        if (( first )); then
            print -r -- "${y}${line}${x}"
            first=0
        else
            print -r -- "$line"
        fi
    done < "$func_dir/$func_name"
    return 0
fi

# Search in /lib directory
local file
local -a comment_block=()

for file in "$lib_dir"/*.zsh(.N); do
    comment_block=()

    while IFS= read -r line; do
        if [[ "$line" =~ "^${func_name}\\s*\\(\\)" ]]; then
            print "${g}${func_name}${x} is a helper function from ${c}/lib/${file:t}${x}"
            local first=1
            for cmt in "${comment_block[@]}"; do
                cmt="${cmt#\#}"
                cmt="${cmt# }"
                if (( first )); then
                    print -r -- "${y}${cmt}${x}"
                    first=0
                else
                    print -r -- "$cmt"
                fi
            done
            return 0
        elif [[ "$line" == \#* ]]; then
            comment_block+=("$line")
        else
            comment_block=()
        fi
    done < "$file"
done

printe "Function '$func_name' not found"
return 1
